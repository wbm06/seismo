      SUBROUTINE AZIDL(SLAT,SLON,ELAT,ELON,DEL,DELS,AZIS,AZIE)
C     AZIS IS AZIMUTH UIT STATION, AZIE UIT EPICENTRUM
C     DELS IS THE DISTANCE OVER THE SURFACE OF AN ELLIPSOIDAL EARTH
C     CALCULATED WITH FORM 29 OF THOMAS, JGR 70, PP 3331-3340, 1965
C     ALLES IN RADIALEN
      COLAT(A)=1.57079632679-ATAN(.993277*TAN(A))
      SCOLAT=COLAT(SLAT)
      ECOLAT=COLAT(ELAT)
      SC1=SIN(SCOLAT)
      SC2=COS(SCOLAT)
      SC3=SIN(SLON)
      SC4=COS(SLON)
      EC1=SIN(ECOLAT)
      EC2=COS(ECOLAT)
      EC3=SIN(ELON)
      EC4=COS(ELON)
      AE=EC1*EC4
      BE=EC1*EC3
      AZI1=(AE-SC3)**2+(BE+SC4)**2+EC2*EC2-2.
      AZI2=(AE-SC2*SC4)**2+(BE-SC2*SC3)**2+(EC2+SC1)**2-2.
      IF(AZI2.EQ.0.) GO TO 10
      AZIS=ATAN2(AZI1,AZI2)
      GO TO 20
   10 AZIS=3.141592653-SIGN(1.570796326,AZI1)
   20 CODELB=SC1*(AE*SC4+BE*SC3)+SC2*EC2
      DEL=ACOS(CODELB)
      AS=SC1*SC4
      BS=SC1*SC3
      AZI1=(AS-EC3)**2+(BS+EC4)**2+SC2*SC2-2.
      AZI2=(AS-EC2*EC4)**2+(BS-EC2*EC3)**2+(SC2+EC1)**2-2.
      IF(AZI2.EQ.0.) GO TO 30
      AZIE=ATAN2(AZI1,AZI2)
      GO TO 40
   30 AZIE=3.141592653-SIGN(1.570796326,AZI1)
   40 SCOLAT=1.57079632679-SLAT
       ECOLAT=1.57079632679-ELAT
      SC1=SIN(SCOLAT)
      SC2=COS(SCOLAT)
      EC1=SIN(ECOLAT)
      EC2=COS(ECOLAT)
      AE=EC1*EC4
      BE=EC1*EC3
      DELBOD=ACOS(SC1*(AE*SC4+BE*SC3)+SC2*EC2)
      COSDEL=COS(DELBOD)
      X1=((SC2+EC2)**2)/(1.+COSDEL)
      X2=((SC2-EC2)**2)/(1.-COSDEL)
      X=X1+X2
      Y=X1-X2
      COTDEL=1./TAN(DELBOD)
      SINDEL=SIN(DELBOD)
      DEL2=DELBOD*DELBOD
      A=64.*DELBOD+16.*DEL2*COTDEL
      D=48.*SINDEL+8.*DEL2/COSDEL
      B=-(D+D)
      E=30.*SIN(DELBOD+DELBOD)
      C=-30.*DELBOD-8.*DEL2*COTDEL-E/2.
      DELS=6378.2064*(DELBOD-.000847518825*(X*DELBOD-3.*Y*SINDEL)+.08978
     #60195E-6*(X*(A+C*X+D*Y)+Y*(B+E*Y)))
      RETURN
      END
